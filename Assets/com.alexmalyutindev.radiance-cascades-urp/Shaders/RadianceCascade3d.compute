#pragma kernel RenderCascade
#pragma kernel MergeCascade

#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
#include "Common.hlsl"

// Layout:
// .______________.
// | +X | +Y | +Z |
// |____|____|____|
// | -X | -Y | -Z |
// |____|____|____|
RWTexture2D<float4> _OutCascade;
int2 _ProbesCount;

float _ProbeSize;
float _CascadeLevel;
float4x4 _View;
float4x4 _ViewProjection;
float4x4 _InvViewProjection;

static const float3 Axes[6] =
{
    float3(1.0f, 0.0f, 0.0f),
    float3(0.0f, 1.0f, 0.0f),
    float3(0.0f, 0.0f, 1.0f),
    float3(-1.0f, 0.0f, 0.0f),
    float3(0.0f, -1.0f, 0.0f),
    float3(0.0f, 0.0f, -1.0f),
};

static const half3x3 Basis[6] =
{
    // +X
    half3x3(
        0.0h, 0.0h, -1.0h,
        0.0h, 1.0h, 0.0h,
        1.0h, 0.0h, 0.0h
    ),
    // +Y
    half3x3(
        0.0h, 0.0h, -1.0h,
        -1.0h, 0.0h, 0.0h,
        0.0h, 1.0h, 0.0h
    ),
    // +Z
    half3x3(
        1.0h, 0.0h, 0.0h,
        0.0h, 1.0h, 0.0h,
        0.0h, 0.0h, 1.0h
    ),
    // -X
    half3x3(
        0.0h, 0.0h, 1.0h,
        0.0h, 1.0h, 0.0h,
        -1.0h, 0.0h, 0.0h
    ),
    // -Y
    half3x3(
        -1.0h, 0.0h, 0.0h,
        0.0h, 0.0h, -1.0h,
        0.0h, -1.0h, 0.0h
    ),
    // -Z
    half3x3(
        -1.0h, 0.0h, 0.0h,
        0.0h, 1.0h, 0.0h,
        0.0h, 0.0h, -1.0h
    ),
};

[numthreads(8,8,1)]
void RenderCascade(uint3 id : SV_DispatchThreadID)
{
    uint2 coords = id.xy;
    if (any(coords > _CascadeBufferSize.xy))
    {
        return;
    }

    float2 sideSize = _CascadeBufferSize.xy / float2(3, 2);

    float2 probeCenterUV = (floor(fmod(coords, sideSize) / _ProbeSize) * _ProbeSize + 0.5f * _ProbeSize) / sideSize;

    int probeSide = dot(floor(coords / sideSize), float2(1, 3));
    float2 indexInProbe = floor(fmod(coords, _ProbeSize));

    float3 direction = float3(0.0f, 0.0f, 0.5f);
    direction.xy += (indexInProbe + 1) / (_ProbeSize + 1.0f) * 2.0f - 1.0f;
    direction = mul(Basis[probeSide], normalize(direction));


    float sceneDepth = SAMPLE_DEPTH_TEXTURE_LOD(_DepthTexture, sampler_PointClamp, probeCenterUV, 0);
    float3 positionWS = ComputeWorldSpacePosition(float4(probeCenterUV, sceneDepth, 1), _InvViewProjection);

    positionWS += _View[2].xyz * 0.1f;

    int2 cascadeRange = CalculateRange(_CascadeLevel);

    float4 color = float4(0, 0, 0, 1);
    for (int i = cascadeRange.x; i < cascadeRange.y; i++)
    {
        float3 ray = positionWS + direction * i * 0.1f;

        float4 positionCS = mul(_ViewProjection, float4(ray, 1));
        float2 screenUV = positionCS.xy / positionCS.w;

        // _OutCascade[coords] = float4(screenUV, 0, 1);
        // return;

        if (any(screenUV < 0 || screenUV > 1)) break;

        float currentDepth = SAMPLE_DEPTH_TEXTURE_LOD(_DepthTexture, sampler_PointClamp, screenUV, 0);
        if (currentDepth > positionCS.z) // INTERSECTION!
        {
            color = SAMPLE_TEXTURE2D_LOD(_ColorTexture, sampler_PointClamp, screenUV, 0);
            color.a = 0.0f;
            break;
        }
    }

    _OutCascade[coords] = color;
}


/////////////
/// MERGE ///
/////////////

float _LowerCascadeLevel;
Texture2D _UpperCascade;
RWTexture2D<float4> _LowerCascade;

static const int Cascade0Size = 1;

float4 AverageAngles(int2 topLeft)
{
    //       * | *
    // * => ---+---
    //       * | *
    float4 radiance = _UpperCascade[topLeft];
    radiance += _UpperCascade[topLeft + int2(1, 0)];
    radiance += _UpperCascade[topLeft + int2(0, 1)];
    radiance += _UpperCascade[topLeft + int2(1, 1)];
    radiance *= 0.25f;

    return radiance;
}

[numthreads(8,8,1)]
void MergeCascade(uint3 id : SV_DispatchThreadID)
{
    int2 coords = id.xy;
    if (any(coords > _CascadeBufferSize.xy))
    {
        return;
    }


    // Calculating dimensions
    int lowerCascadeDim = Cascade0Size * pow(2, _LowerCascadeLevel);
    int upperCascadeDim = Cascade0Size * pow(2, _LowerCascadeLevel + 1);

    int2 upperCoords = floor(coords / upperCascadeDim) * upperCascadeDim; // TODO: !!!
    float4 radiance = _LowerCascade[coords]; // Radiance from lower cascade

    //Calculate the position of the center of the lower probe
    int2 lowerCascadeIndex = floor(coords / lowerCascadeDim);
    int2 lowerCascadeCenterPos = lowerCascadeIndex * lowerCascadeDim + (lowerCascadeDim / 2);

    int2 upperCascadeIndex = floor((lowerCascadeIndex - 0.5) / 2.0);
    int2 upperCascadeOriginPos = upperCascadeIndex * upperCascadeDim;
    int2 upperCascadeCenterPos = upperCascadeOriginPos + upperCascadeDim * 0.5f;


    float4 a = AverageAngles(upperCoords);
    float4 b = AverageAngles(upperCoords + int2(upperCascadeDim, 0));
    float4 c = AverageAngles(upperCoords + int2(0, upperCascadeDim));
    float4 d = AverageAngles(upperCoords + int2(upperCascadeDim, upperCascadeDim));


    float2 weight = (lowerCascadeCenterPos - upperCascadeCenterPos) / float(upperCascadeDim);
    float4 m0 = lerp(a, b, weight.x);
    float4 m1 = lerp(c, d, weight.x);

    float4 upperRadiance = lerp(m0, m1, weight.y);

    radiance.rgb += upperRadiance.rgb * radiance.a;
    radiance.a *= upperRadiance.a;

    _LowerCascade[id.xy] = radiance;
}
